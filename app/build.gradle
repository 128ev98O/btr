apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def signingProperties = new Properties()
def signingStoreFile = file('./blocktopograph.jks')
def signingPropertiesFile = rootProject.file("signing.properties")
if (signingPropertiesFile.exists()) {
    signingProperties.load(new FileInputStream(signingPropertiesFile))
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId 'nguyenduck.blocktopograph'
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "Beta 1.9.7"
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled true
    }
    configurations {
        implementation.exclude group: 'org.jetbrains', module: 'annotations'
    }

    if (signingStoreFile.exists()) {
        signingConfigs {
            release {
                storeFile file('./blocktopograph.jks')
                storePassword signingProperties['signingStorePassword'] ?: rootProject.signingStorePassword
                keyAlias signingProperties['signingKeyAlias'] ?: rootProject.signingKeyAlias
                keyPassword signingProperties['signingKeyPassword'] ?: rootProject.signingKeyPassword
            }
        }
    }

    buildTypes {

        debug {
//            firebaseCrashlytics {
//                mappingFileUploadEnabled false
//            }
            postprocessing {
                removeUnusedCode true
                obfuscate false
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
            //multiDexEnabled true
            ext.alwaysUpdateBuildId = false
        }

        release {
            if (signingStoreFile.exists()) {
                signingConfig signingConfigs.release
            }
            postprocessing {
                removeUnusedCode true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    namespace 'com.mithrilmania.blocktopograph'
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.so', '*.aar'], dir: 'libs')
    implementation 'com.google.firebase:firebase-crashlytics:18.3.3'
    implementation 'com.google.firebase:firebase-analytics:21.2.0'
    testImplementation 'junit:junit:4.13.2'
    implementation project(':leveldb')
    implementation project(':tileview')
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.github.bmelnychuk:atv:1.2.9'
    implementation 'com.github.woxthebox:draglistview:1.7.2'
    implementation 'com.andreabaccega:android-edittext-validator:1.3.5'
    implementation 'org.jetbrains:annotations-java5:24.0.0'
    implementation 'com.tomergoldst.android:tooltips:1.1.1'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    //implementation 'com.github.MikeOrtiz:TouchImageView:2.1.1'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.florent37:expansionpanel:1.2.4'
    implementation 'com.github.jbvincey:nestedradiobutton:1.1'
    implementation 'net.lingala.zip4j:zip4j:2.11.3'
    // do NOT accidentally "up"-grade to the 2003 version
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.1'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.jetbrains:annotations:24.0.0'
}

